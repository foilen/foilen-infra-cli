buildscript {
  ext {
    springBootVersion = '2.1.8.RELEASE'
  }
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
  }
}

plugins {
  id "com.jfrog.bintray" version "1.8.4"
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'maven'
apply plugin: 'maven-publish'

group = 'com.foilen'
version = System.getenv('VERSION')?:'master-SNAPSHOT'

repositories {
  mavenLocal()
  jcenter()
  mavenCentral()
  maven {
    url 'https://dl.bintray.com/foilen/maven/'
  }
}

sourceCompatibility = 1.11

dependencies {
  compile 'com.foilen:jl-smalltools:0.47.1'
  compile "javax.xml.bind:jaxb-api:2.3.1"
  compile 'com.jcraft:jsch:0.1.54'
  compile 'com.foilen:foilen-infra-api:0.6.1'
  compile 'com.foilen:foilen-infra-plugins-core:0.8.6'
  compile 'org.freemarker:freemarker:2.3.23'
  compile 'org.jsoup:jsoup:1.11.3'
  compile 'org.springframework.data:spring-data-jpa'
  compile('org.springframework.shell:spring-shell-starter:2.0.1.RELEASE')
  testCompile('org.springframework.boot:spring-boot-starter-test')
}

jar {
	enabled = true
}

bootJar {
	classifier = 'boot'
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives sourcesJar
  archives javadocJar
}

bintray {
  user = System.getenv('BINTRAY_USER')
  key = System.getenv('BINTRAY_KEY')

  publish = true

  pkg {
    repo = 'maven'
    userOrg = 'foilen'
    name = 'com.foilen:foilen-infra-cli'
    licenses = ['MIT']
    vcsUrl = 'https://github.com/foilen/foilen-infra-cli.git'
  }

  publications = ['MyPublication']
}

publishing {
  publications {
    MyPublication(MavenPublication) {
      from components.java
      artifact sourcesJar
      artifact javadocJar
      groupId 'com.foilen'
      artifactId 'foilen-infra-cli'
    }
  }
}
