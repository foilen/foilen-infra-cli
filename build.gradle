plugins {
  id "com.github.ben-manes.versions" version "$versionsVersion"
  id 'io.spring.dependency-management' version "$springDependencyManagementVersion"
  id 'org.springframework.boot' version "$springBootVersion"
}

apply plugin: 'eclipse'
apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

group = 'com.foilen'
version = System.getenv('VERSION')?:'master-SNAPSHOT'
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

repositories {
  mavenLocal()
  mavenCentral()
}

sourceCompatibility = 11

eclipse {
  classpath {
    downloadSources = true
    downloadJavadoc = true
  }
}

dependencies {
  api "com.foilen:jl-smalltools:$smalltoolsVersion"
  api "dnsjava:dnsjava:$dnsJavaVersion"
  api "javax.xml.bind:jaxb-api"
  api "com.jcraft:jsch:$jschVersion"
  api "com.foilen:foilen-infra-api:$foilenInfraApiVersion"
  api "com.foilen:foilen-infra-plugin-core:$foilenInfraPluginVersion"
  api "com.foilen:foilen-infra-plugins-core:$foilenInfraPluginsCoreVersion"
  api "org.apache.httpcomponents:httpclient"
  api "org.freemarker:freemarker"
  api "org.jsoup:jsoup:$jsoupVersion"
  api "org.springframework.data:spring-data-jpa"
  api "org.springframework.shell:spring-shell-starter:$springShellVersion"
  api "org.springframework:spring-web"
  
  testImplementation "junit:junit"
  testImplementation "org.springframework.boot:spring-boot-starter-test"
}

jar {
	enabled = true
}

bootJar {
	classifier = 'boot'
}

java {
  withJavadocJar()
  withSourcesJar()
}

publishing {
  repositories {
    maven {
      url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
      credentials {
        username = System.getenv('OSSRH_USER')
        password = System.getenv('OSSRH_PASS')
      }
    }
  }

  publications {
    mavenJava(MavenPublication) {
      pom {
        name = "foilen-infra-cli"
        description = 'A CLI to help managing the infrastructure'
        url = 'https://github.com/foilen/foilen-infra-cli'
        from components.java
        
        scm {
          connection = "scm:git:git@github.com:foilen/foilen-infra-cli.git"
          url = "https://github.com/foilen/foilen-infra-cli"
        }

        licenses {
          license {
            name = "MIT"
            url = "https://opensource.org/licenses/MIT"
          }
        }

        developers {
          developer {
            id = 'foilen'
            name = 'Simon Levesque'
            email = 'simon@foilen.com'
          }
        }
      }
    }
  }
}

signing {
  sign publishing.publications.mavenJava
}
tasks.withType(Sign) {
  onlyIf { isReleaseVersion }
}
