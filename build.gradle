buildscript {
  repositories {
    mavenLocal()
    jcenter()
  }
}
plugins {
  id "com.jfrog.bintray" version "$bintrayVersion"
  id 'io.spring.dependency-management' version "$springDependencyManagementVersion"
  id 'org.springframework.boot' version "$springBootVersion"
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'maven-publish'

group = 'com.foilen'
version = System.getenv('VERSION')?:'master-SNAPSHOT'

repositories {
  mavenLocal()
  jcenter()
  mavenCentral()
  maven {
    url 'https://dl.bintray.com/foilen/maven/'
  }
}

sourceCompatibility = 1.11

eclipse {
  classpath {
    downloadSources = true
    downloadJavadoc = true
  }
}

dependencies {
  compile "com.foilen:jl-smalltools:$smalltoolsVersion"
  compile "dnsjava:dnsjava:$dnsJavaVersion"
  compile "javax.xml.bind:jaxb-api"
  compile "com.jcraft:jsch:$jschVersion"
  compile "com.foilen:foilen-infra-api:$foilenInfraApiVersion"
  compile "com.foilen:foilen-infra-plugin-core:$foilenInfraPluginVersion"
  compile "com.foilen:foilen-infra-plugins-core:$foilenInfraPluginsCoreVersion"
  compile "org.freemarker:freemarker"
  compile "org.jsoup:jsoup:$jsoupVersion"
  compile "org.springframework.data:spring-data-jpa"
  compile "org.springframework.shell:spring-shell-starter:$springShellVersion"
  
  testCompile "junit:junit"
  testCompile "org.springframework.boot:spring-boot-starter-test"
}

jar {
	enabled = true
}

bootJar {
	classifier = 'boot'
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives sourcesJar
  archives javadocJar
}

bintray {
  user = System.getenv('BINTRAY_USER')
  key = System.getenv('BINTRAY_KEY')

  publish = true

  pkg {
    repo = 'maven'
    userOrg = 'foilen'
    name = 'com.foilen:foilen-infra-cli'
    licenses = ['MIT']
    vcsUrl = 'https://github.com/foilen/foilen-infra-cli.git'
  }

  publications = ['MyPublication']
}

publishing {
  publications {
    MyPublication(MavenPublication) {
      from components.java
      artifact sourcesJar
      artifact javadocJar
      groupId 'com.foilen'
      artifactId 'foilen-infra-cli'
    }
  }
}
